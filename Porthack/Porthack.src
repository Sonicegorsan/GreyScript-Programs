//command scanlib

//PRE CODE INITIALIZATION PART
//----------------------------------------------------
//Инициализация класса уязвимость
Vulnerability = { "memory_address":null,"variable_name":null,"type":null }

Vulnerability.get_data = function()
	return "<color=orange><b>"+typeof(self.type)+"</b></color>" + " " + self.memory_address + " " + self.variable_name 
end function

//Инициализация функций

//Функция Подключения библиотеки metaxploit
load_metaxploit = function()
	metaxploit = include_lib("/lib/metaxploit.so")
	if not metaxploit then 
		metaxploit = include_lib(current_path + "/metaxploit.so")
	end if
	if not metaxploit then exit("Error: Missing metaxploit library")
	return metaxploit
end function

//Функция Подключения библиотеки cryptools
load_cryptools = function()
	cryptools = include_lib("/lib/crypto.so")
	if not cryptools then 
		cryptools = include_lib(current_path + "/crypto.so")
	end if
	if not cryptools then exit("Error: Missing crypto library")
	return cryptools
end function

//Информация о сканируемой библиотеке
display_library_data = function(metaLib)
	print("Scan library data.\nName: " + metaLib.lib_name + "\nVersion: " + metaLib.version)
end function

//Обработчик уязвимостей типа "File"
handle_file_payload = function(file)
	decrypt_file = function(content)
		info = ""
		decrypted_strings_index = 1
		for string in content.split(char(10))
			split_string = string.split(":")
			if split_string.len == 2 then
				info = info + decrypted_strings_index + ")" + split_string[0] + "@" + cryptools.decipher(split_string[1]) + "\n"
				decrypted_strings_index = decrypted_strings_index + 1
			end if
		end for
		return info
	end function

	show_file_info = function()
		show_condition = function(condition)
			if condition then return "<color=green><b>True</b></color>"
			if not condition then return "<color=red><b>False</b></color>"
		end function
		print("Binary: " + show_condition(file.is_binary))
		print("Folder: " + show_condition(file.is_folder))
		print("Path: " + file.path)
	end function

	parse_file = function(file_name)
		files_in_folder = file.get_files
		for object in files_in_folder
			if object.name == file_name then return object
		end for
		return null
	end function
	
	action_index = function(user_action)
		if user_action == "help" then return 1
		if user_action == "ls" then return 2
		if user_action == "cat" then return 3
		if user_action == "rm" then return 4
		if user_action == "cd" then return 5
		if user_action == "getpass" then return 6
		return null
	end function

	help = function(placeholder)
		print( "<b>help</b> - show this message" )
		print( "<b>exit</b> - exit file payload")
		print( "<b>ls</b> - show file content (files if folder/content if file)")
		print( "<b>cat (-d optional for decryption) [file]</b> - show file content")
		print( "<b>rm [file]</b> - delete file")
		print( "<b>cd [folder]</b> - change currnet file (works with all files)")
		print( "<b>getpass (-u optional for undecrypted) [mail/bank/passwd]<b> - shows all bank/mail/passwd files in decrypted view")
	end function

	ls = function(placeholder)
		
		if file.is_folder then
			print("Folder contains: ")
			objects_in_folder = file.get_files
			objects_in_folder = objects_in_folder + file.get_folders
			for object in objects_in_folder
				if not object.is_folder then
					print("<color=yellow><b>"+object.name+"</color></b>")
				else
					print("<color=orange><b>"+object.name+"/</color></b>")
				end if
			end for
	end function
	
	cat = function(argv)
		if argv.len == 0 then
			print("Not enough arguments")
			return null
		end if
	
		decrypt = false
		if argv.len == 1 then
			target_file = parse_file(argv[0])
		else if argv.len == 2 and argv[0] == "-d" then
			decrypt = true
			target_file = parse_file(argv[1])
		end if
		
		if not target_file then 
			print("File not found")
			return null
		else if not target_file.has_permission("r") then 
			print("You don't have access to this file")
			return null
		else if target_file.is_binary then
			print("Can't show content of a binary file")
			return null
		else if decrypt then 
			print(decrypt_file(target_file.get_content))
		else
			print(target_file.get_content)
		end if
	end function

	rm = function(argv)
		if argv.len == 0 then
			print("Not enough arguments")
			return null
		else
			target_file = parse_file(argv[0])
			if target_file.has_permission("w") then
				target_file.delete
			else
				print("You can't delete this file (Not enough permissions)")
			end if
		end if
	end function
	
	cd = function(argv)
		if argv.len == 0 then
			print("Not enough arguments")
			return null
		end if
		path = argv[0]
		if path == ".." and file.path != "/" then			
			outer.file = file.parent
			return null
		else
			objects_in_folder = file.get_folders
			for new_file in objects_in_folder
				if new_file.name == path or (new_file.name + "/") == path or new_file.name[:path.len] == path then
					outer.file = new_file
				end if
			end for
		end if
			
	end function

	getpass = function(argv)
	
		get_source_directory = function(file)
			while file.path != "/"
				file = file.parent
			end while
			return file
		end function

		parse_user_folder = function(file_object,parsing_file_name)
			for user_folder_content in file_object.get_folders
				if user_folder_content.name == "Config" then confing_folder = user_folder_content
			end for
			for confing_folder_content in confing_folder.get_files
				if confing_folder_content.name == parsing_file_name then parsed_file = confing_folder_content
			end for
			return parsed_file
		end function	

		decypher = true
		if argv.len > 2 or (argv[0] == "-u" and argv.len != 2) then 
			print("Wrong arguments amount")
			return null
		else if argv[0] == "-u" and argv.len == 2 then
				decypher = false
				argv[0] = argv[1]
		end if
		
		if argv[0] != "mail" and argv[0] != "bank" and argv[0] != "passwd" then
			print("Wrong argumen name,should be mail/bank/passwd")
			return null
		end if
		if argv[0]=="passwd" then
			source_directory = get_source_directory(outer.file)
			for source_dir_folder in source_directory.get_folders
				if source_dir_folder.name == "etc" then etc_folder = source_dir_folder
			end for
			remembered_file = outer.file
			outer.file = etc_folder //parse_file only works with file that was passed to handler function,so we need to temporary replace our file object with /etc/ folder file object
			passwd_file = parse_file("passwd")	
			outer.file = remembered_file
			if passwd_file.get_content == null then
				print("Cannot access passwd file")
				return null
			end if
			if decypher then 
				print(decrypt_file(passwd_file.get_content))
			else
				print(passwd_file.get_content)
			end if
			

		else if argv[0]=="mail" then
			source_directory= get_source_directory(outer.file)
			for source_dir_folder in source_directory.get_folders
				if source_dir_folder.name == "home" then home_dir = source_dir_folder
			end for
			parsed_files_amount = 0
			print("Found users in home folder.")
			for user_folder in home_dir.get_folders
				if user_folder.name != "guest" and  user_folder.name != "root" then
					parsed_file = parse_user_folder(user_folder,"Mail.txt")
					if parsed_file != null then
						parsed_files_amount = parsed_files_amount + 1
						if decypher then 
							print("-"*15 + "\n" + parsed_files_amount + ")" + user_folder.name + " Mail.txt\n" + decrypt_file(parsed_file.get_content) + "-"*15) 
						else
							print("-"*15 + "\n" + parsed_files_amount + ")" + user_folder.name + " Mail.txt\n" + parsed_file.get_content + "-"*15) 
						end if
					end if
				end if
			end for

			
		else if argv[0]=="bank" then
			source_directory= get_source_directory(outer.file)
			for source_dir_folder in source_directory.get_folders
				if source_dir_folder.name == "home" then home_dir = source_dir_folder
			end for
			parsed_files_amount = 0
			for user_folder in home_dir.get_folders
				if user_folder.name != "guest" and  user_folder.name != "root" then
					parsed_file = parse_user_folder(user_folder,"Bank.txt")
					if parsed_file != null then
						parsed_files_amount = parsed_files_amount + 1
						if decypher then 
							print("-"*15 + "\n" + parsed_files_amount + ")" + user_folder.name + " Bank.txt\n" + decrypt_file(parsed_file.get_content) + "-"*15) 
						else
							print("-"*15 + "\n" + parsed_files_amount + ")" + user_folder.name + " Bank.txt\n" + parsed_file.get_content + "-"*15) 
						end if
					end if
				end if
			end for
		end if
	end function
	action = { 1:@help,2:@ls,3:@cat,4:@rm,5:@cd,6:@getpass }
	
	while true
		user_action = user_input("<b>" + file.path + "@Filesystem></b>")
		if user_action == "exit" then break
		if file == null then
			print("File was deleted, now you can only exit")
			continue
		end if
		user_action = user_action.split(" ") 
		user_action_index = action_index(user_action[0])
		user_action.pull //Removes argument name and now it is an array of function args
		argv = user_action
		if user_action_index != null then
			action[user_action_index](argv)
		else 
			print("Unknown command, type <b>help</b> for help.")
		end if
	end while
end function

//Обработчик уязвимостей типа "computer"
handle_computer_payload = function(computer)
	action_index = function(user_action)
		if user_action == "help" then return 1
		if user_action == "ps" then return 2
		if user_action == "passwd" then return 3
		if user_action == "file" then return 4
		return null
	end function

	help = function()
		print( "help - show this message" )
		print( "ps - show all system proceses")
		print( "passwd [username] [password] - change password (root required)")
		print( "file [absolute_path_to_file] - gives you the File object with actions interpreter to it")
		print( "exit - exit computer payload")
	end function

	ps = function()
		print(format_columns(computer.show_procs))
	end function

	passwd = function(user,password)
		success = computer.change_password(user,password)
		if success then print("<color=green><b>" + user + " password was successfuly changed to: " + password + "</b></color>")
		if not success then print("<color=red><b>You don't have root priviliges to do that</b></color>")
	end function

	file = function(file_path)
		file = computer.File(file_path)
		if file != null then
			handle_file_payload(file)
		else
			print("File does not exist.")
		end if
	end function

	action = { 1:@help,2:@ps,3:@passwd,4:@file }
	while true
		user_action = user_input(computer.public_ip + "@Computer>")
		if user_action == "exit" then break
		user_action = user_action.split(" ") 
		user_action_index = action_index(user_action[0])
		user_action.pull //Removes argument name and now it is an array of function args
		if user_action_index != null then
			if user_action_index == action_index("passwd") then
				if user_action.len != 2 then
					print("<color=red><b>Not enough arguments.</b></color>")
					continue
				end if
				action[user_action_index](user_action[0],user_action[1])
			else if user_action_index == action_index("file") then
				if user_action.len != 1 then
					print("<color=red><b>Not enough arguments.</b></color>")
					continue
				end if
				action[user_action_index](user_action[0])
			else
				action[user_action_index]
			end if
		else 
			print("Unknown command, type help for help.")
		end if
	end while
end function

handle_payload = function(metaLib,vulnerability)
	if typeof(vulnerability.type)=="computer" then handle_computer_payload(vulnerability.type)
    if typeof(vulnerability.type)=="shell" then vulnerability.type.start_terminal
    if typeof(vulnerability.type)=="file" then handle_file_payload(vulnerability.type)
    if typeof(vulnerability.type)=="number" then 
		metaLib.overflow(vulnerability.memory_address,vulnerability.variable_name,user_input("New password:"))
	end if
end function
//-----------------------------------------------

//PROGRAM START
if not get_shell.host_computer.is_network_active then exit("No internet access.")

//Подключение библиотеки
metaxploit = load_metaxploit
cryptools = load_cryptools

//Обработка параметров сканера уязвимостей в сетевых библиотеках
address = null
port = null
additional_parameter = null
if params.len > 1 then exit("You need to provide an address of the machine that will be Porthacked. Or start it without params to crack localy.")

if params.len == 1 then
	if is_valid_ip(params[0]) then
		address = params[0]
		isLanIp = is_lan_ip( address )
		if isLanIp then
   			router = get_router;
		else 
			router = get_router( address )
		end if
		if router == null then exit("Ip address not found.")
		
		if not isLanIp then
			ports = router.used_ports
		else
			ports = router.device_ports(address)
		end if
		if ports == null then exit("Ip address not found")
		if typeof(ports) == "string" then exit(ports)
	
	//Выводим все порты, которые можно взломать
	info = "PORT STATUS LIBRARY VERSION IP"
	
	while true //THIS WHILE IS TO JUST MAKE BREAK FROM IT, IT WORKS ONLY ONCE
		net_session = metaxploit.net_use( address )
		if not net_session then break
		metaLib = net_session.dump_lib
		info = info + "\n0: open " + metaLib.lib_name + " " + metaLib.version + " " + address
		break
	end while
	ports_list = [0]
	for port in ports
		ports_list.push(port.port_number)
		port_status = "open"
		if(port.is_closed and not isLanIp) then
			port_status = "closed"
		end if
		if port_status != "closed" then
			net_session = metaxploit.net_use( address,port.port_number )
			if not net_session then 
				port_status = "firewall"
				service_info = router.port_info(port)
			else
				metaLib = net_session.dump_lib
				service_info = metaLib.lib_name + " " + metaLib.version
			end if
		else
			service_info = router.port_info(port)
		end if
		lan_ips = port.get_lan_ip
		info = info + "\n" + port.port_number + ": " + port_status + " " + service_info + " " + lan_ips
	end for
	//Раскрашиваем вывод доступных для взлома портов
	info = format_columns(info)
	for str in info.split(char(10))
		if str.indexOf("PORT") != null then str = "<color=yellow>"+str+"</color>"
		if str.indexOf("open") != null then str = str.replace("open","<color=green><b>open</b></color>") 
		if str.indexOf("firewall") != null then str = str.replace("firewall","<color=orange><b>firewall</b></color>") 
		if str.indexOf("closed") != null then str = str.replace("closed","<color=red><b>closed</b></color>") 
		print str
	end for
	while true
		port = user_input("<b>Choose port service to crack: </b>").to_int
		if ports_list.indexOf(port) != null then
			break
		else 
			print("Port not in list, try again")
		end if
	end while
	if port == 0 then additional_parameter = user_input("<b>Specify LAN address for a bounce attack:</b>")
	else 
		exit("Incorrect IP address")
	end if
end if


//Получение снимка библиотеки из сети,либо файла
if address != null then
	net_session = metaxploit.net_use( address,port )
	if not net_session then exit("Error: can't connect to net session")
	metaLib = net_session.dump_lib
else
	local_libraries = [ "kernel_module.so","router_module.so","init.so","net.so","crypto.so","blockchain.so","aptclient.so","metaxploit.so" ]
	found_libraries = []
	library_index = 1
	for library in local_libraries
		if get_shell.host_computer.File("/lib/" + library) != null then
			found_libraries.push(library)
			print(library_index + ": " + library)
			library_index = library_index + 1
		end if
	end for
	while true
		lib_name_index = user_input("<b>Choose library index to crack: </b>").to_int
		if found_libraries.hasIndex(lib_name_index-1) != 0 then break
	end while
	libFile = get_shell.host_computer.File("/lib/" + found_libraries[lib_name_index-1])
	if not libFile then exit("For some reason " + libName + " was not found.")
	metaLib = metaxploit.load(libFile.path)
end if

//Даные о библиотеке
display_library_data metaLib

//Скан и вывод всех уязвимостей
listMem = metaxploit.scan(metaLib)
vulnerabilities_list = []
variables_parsed = 1
if listMem.len == 0 then exit("Scan completed: No issues detected.")
for itemMem in listMem
	scan_data = metaxploit.scan_address(metaLib, itemMem)
	for string in scan_data.split(char(10))
		if string.indexOf("Unsafe check") != null and string.indexOf("<")!=null then
			vulnerability = new Vulnerability
			vulnerability.memory_address = itemMem
			vulnerability.variable_name = string[string.indexOf(">")+1:string.indexOf("</")]
			if additional_parameter != null then
				vulnerability.type = metaLib.overflow(vulnerability.memory_address,vulnerability.variable_name,additional_parameter )
			else 
				vulnerability.type = metaLib.overflow(vulnerability.memory_address,vulnerability.variable_name)
			end if
			if typeof(vulnerability.type) != "null" then
				text_color = "<color=green>"
				vulnerabilities_list.push(vulnerability)
			else
				text_color = "<color=red>"
			end if
			print(text_color + variables_parsed + ": " + vulnerability.memory_address + " " + vulnerability.variable_name + " " + typeof(vulnerability.type)+"</color>\n")
			variables_parsed = variables_parsed + 1
		end if 
	end for
end for

user_input_value = -1
while user_input_value != 0
	print("<color=yellow><b>0: Exit</b></color>")
	index_value = 1
	for i in vulnerabilities_list
		print("<color=green>"+index_value+"</color>" + ": " + i.get_data)
		index_value = index_value + 1
	end for
	user_input_value = user_input("What to do:").to_int
	if user_input_value != 0 then
		handle_payload(metaLib,vulnerabilities_list[user_input_value-1])
	end if
end while


